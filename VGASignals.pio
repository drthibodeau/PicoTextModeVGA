
.define pixelwait 18
.define CS_ENABLE 0             

; !!!! use directives here : !!!!!
; hardcoded gpio pins:
; gpio 16 is clock

; IRQ 5 controls vsync line count
; counting pixels (or transition) as if value is sent at end of instruction cycle. i.e. sets are on first instruction of phase
.program SYNC

.wrap_target
; ---------------------------- HSYNC -------------------------------------  

PUBLIC hsync_start:                         ; PUBLIC will make the label visible under program struct in C     
    
    set pins 1                              
    
hsync_loop: 

;  ACTIVE AREA = 32*24 + 1 + 32 = 801 cycles 
    set y, 31 [31]                          ; 32 cyles, 1st pixel sent on this instruction   
h_active_loop:                              
    jmp y-- h_active_loop [23]              ; 32*24 cycles      
    nop [20]                                ; 1 cycle + 20 for fornt Porch. last pixel sent at end of this instruction (i think)
    
; FRONT PORCH = 20 cycles

; HSYNC PULSE = 1 + 19 + 100 = 120 cycles                                           
    set pins 0                              ;  1 cycle. last instruction of front porch, VSYNC waiting for this signal to start code for each line                                      
    set y, 24 [18]                          ;  19 cycles
h_pulse_loop:                                
    jmp y-- h_pulse_loop [3]                ; 25*4 = 100   
       
; BACK PORCH = 32 + 25 + 2 + 1 = 60 cycles    
    set pins 1 [31]                        ; 32 cycle                      
    nop [24]                                ; 25
    irq clear 5 [1]                         ; 2 cycles. vsync prog tests if last active vert line (on the DELAY cycle)                                               
    jmp hsync_loop                          ; 1 cycle.               


; ------------------------------- VSYNC --------------------------------------
; timing notes: set pins = 3 has to come on the second instruction after irq wait 5. This sets gpio "pixelwait" high 

PUBLIC vsync_start:                         ; PUBLIC will make the label visible under program struct in C     

    out x, 32                               ; store active area line count (-1  for 0th iteration) 
 
 vsync_loop:    
    mov y, x                                ; (re)load counter

v_active_loop:     
    set pins 3                              ; on loop back this is last hsync instruction. Start pixel output on next pixel sm cycle.   
    set pins 1                              ; toggle pixel enable   
    irq wait 5                              ; set and wait on hsync to clear irq towards end of line   
    jmp y-- v_active_loop           

    set y, 9                               
v_frontp_loop:                              ; FRONT PORCH 10 lines
    irq wait 5                              ; set and wait on hsync to clear irq towards end of line        
    jmp y-- v_frontp_loop                   

; VSYNC PULSE
    nop                                     ; the third cycle after irq wait is first cycle of new line. should burn one cycle  
    set pins 0
    irq wait 5                              ; VSYNC pulse 2 lines
    irq wait 5 [1]                     
    
    set y, 31                               ; BACK PORCH 33 lines,                                  
    set pins 1                              ; set pin on first cycle of new line (third cycle after wait) 
   
    irq wait 5                              ; first line of back porch
v_backp_loop:                               ; remaining 32 lines of back porch
    irq wait 5                              ; set and wait on hsync to clear irq towards end of line        
    jmp y-- v_backp_loop   

    set pins 3                              ; last hsync instruction. Start pixel output routine on next pixel cycle.  Need pixel start delay to accommodate jump to hsync loop 
    jmp vsync_loop 
   
   .wrap


; GPIO "pixelwait" controls pixel output, turned on and off by vsync program
; counting pixels (or transition) as if value is sent at end of instruction cycle. i.e. sets are on first instruction of phase
.program PIXEL
 
.wrap_target  

PUBLIC pixelData:
    out x,32               ; this is the loop size (using autopull)
  
pixel_reset:
    mov y,x
    wait 1 gpio pixelwait 
    nop                   ; adds a delay so that first pixel out lines up with hysnc active count. On test monitor this gives the lowest clock phase adjustment 
pixel_loop:  
    out pins, 1 [3]        ; 5 cycles of output until pixel change (3 delays + jmp + next out pins). On test monitor this is bang on according to clock adjustment
    jmp y-- pixel_loop
    set pins 0
    jmp pixel_reset
    

PUBLIC dataRead:
 
    wait 1 gpio CS_ENABLE 
edgeFall:
    wait 0 gpio CS_ENABLE [1]       ; falling edge detection CS, need a one cycle delay
    in pins, 1                      ; read the CS enable line
    mov x, isr                      ; this will reset ISR
    jmp x-- edgeFall                ; catch false falling edge. guards against noisy signals  

  ;  wait 0 gpio 16         ; wait for falling clock edge   
   ; set x, 
 ;  in pins, 9             ; read all pins to get the R/W signal (bit 8)
  ;  wait 1 gpio 16         ; wait for rising clock edge
  ;  in pins, 9            ; read all pins to get data (bits 0-7)
   
    irq set 1             ; system level interrupt on IRQ 1 
    jmp dataRead 


   .wrap


